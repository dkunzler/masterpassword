apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    lintOptions {
        disable 'MissingTranslation'
    }

    signingConfigs {
        release {
            storeFile file("../release.jks")
            keyAlias "release"
        }
        debug {
            storeFile file("../debug.keystore")
        }
    }

    defaultConfig {
        applicationId "de.devland.masterpassword"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 50
        versionName "1.13.4"
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}


repositories {
    flatDir {
        dirs 'libs'
    }
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    // import, export
    compile 'com.nononsenseapps:filepicker:2.5.2'
    compile 'com.google.code.gson:gson:2.8.0' // Apache v2.0

    // styling
    compile 'com.github.emanzanoaxa:RippleEffect:52ea2a0ab6'
    compile(name:'showcaseview-git', ext:'aar') // Apache v2.0
    compile(name:'recyclerviewfastscroller-release', ext:'aar') // Apache v2.0
    compile 'com.android.support:appcompat-v7:26.0.1'
    compile 'com.android.support:design:26.0.1'
    compile 'com.android.support:cardview-v7:26.0.1'
    compile 'com.android.support:recyclerview-v7:26.0.1'

    // annotation processors
    annotationProcessor 'org.projectlombok:lombok:1.14.0' // MIT
    annotationProcessor 'de.devland.esperandro:esperandro:2.5.1' // Apache v2.0
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // tools
    compile 'com.jakewharton:butterknife:8.8.1' // Apache v2.0
    compile 'com.squareup:otto:1.3.8' // Apache v2.0
    compile files('libs/lombok-api-1.14.0.jar') // MIT
    compile 'com.github.satyan:sugar:1.5' // MIT
    compile 'de.devland.esperandro:esperandro-api:2.5.1' // Apache v2.0
    compile('de.devland.esperandro:esperandro-gson-addon:2.5.1') { // Apache v2.0
        exclude module: 'gson' // problems while dexing
    }

    // MasterPassword dependencies
    compile('com.lyndir.masterpassword:masterpassword-model:2.4.1-java') { // GPL v3
//    compile('com.github.dkunzler.MasterPasswordFork:masterpassword-model:d854f19') {
//        exclude module: 'jsr305' // problems while dexing
        exclude module: 'cglib'
    }
    compile('com.lyndir.masterpassword:masterpassword-algorithm:2.4.1-java') { // GPL v3
//    compile('com.github.dkunzler.MasterPasswordFork:masterpassword-algorithm:d854f19') {
//        exclude module: 'jsr305' // problems while dexing
        exclude module: 'cglib'
    }
    compile 'cglib:cglib:3.2.4'
    compile 'org.slf4j:slf4j-android:1.7.13' // MIT

    compile files('libs/inputstickapi-20170113.jar') // no license
}
